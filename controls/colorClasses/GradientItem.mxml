<?xml version="1.0" encoding="utf-8"?>
<mx:Box xmlns:mx="http://www.adobe.com/2006/mxml"
	width="100%" height="100%"

	creationComplete="onCreationComplete()"
 	dataChange="onDataChange(event)">


	<mx:Script>
    <![CDATA[

		[Bindable]
		private var gradient:CloudGradient = null; // temp var, this class just renders it


		private var backgroundMatrix:Matrix = new Matrix();

		private var gradientMatrix:Matrix = new Matrix();


		private function onCreationComplete():void
		{
			DrawGradient();
			this.parentDocument.addEventListener("gradientParamChange", onGradientParamChange);
		}

		private function onDataChange(event:Event):void
		{
			gradient = this.data as CloudGradient;
			DrawGradient();
		}

		private function onGradientParamChange(event:GradientEvent):void
		{
			if(gradient == event.gradient)
			{
				gradient = event.gradient;
				DrawGradient();
			}
		}


		private const hOffset:int = 2;
		private const vOffset:int = 0;

		public function DrawGradient():void
		{
			if(!gradient)
				return;

			backgroundMatrix.createGradientBox(this.width-hOffset*2, this.height-1, 0, hOffset, 0);

			gradientMatrix.createGradientBox(this.width-hOffset*2, this.height-1, gradient.rotation, hOffset, 0);

			this.graphics.clear();
			this.graphics.lineStyle(1, 0x000000, 1.0);

			// Background bitmap fill
			this.graphics.beginBitmapFill(Globals.g_assets.backgroundBmp.bitmapData, backgroundMatrix, true, false);
			this.graphics.drawRect(hOffset,0, this.width-hOffset*2, this.height-1);
			this.graphics.endFill();

			// Gradient fill
			this.graphics.beginGradientFill(gradient.gradientType,
											gradient.colors,
											gradient.alphas,
											gradient.ratios,
											gradientMatrix,
											gradient.spreadMethod,
											gradient.interpolationMethod,
											gradient.focalPointRatio);

			this.graphics.drawRect(hOffset, 0, this.width-hOffset*2, this.height-1);
			this.graphics.endFill();
		}

    ]]>
    </mx:Script>


</mx:Box>
